apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
	experimental = true
}

android {
	compileSdkVersion 27
	buildToolsVersion "27.0.3"
	defaultConfig {
		applicationId "net.frju.flym"
		minSdkVersion 21
		targetSdkVersion 27
		versionCode 12
		versionName "2.0.9"
	}

	lintOptions {
		// if true, stop the gradle build if errors are found
		abortOnError true
	}

	Properties properties = new Properties()
	properties.load(project.rootProject.file('local.properties').newDataInputStream())

	signingConfigs {
		debug {
			if (properties.getProperty("KEYSTORE_PATH") != null) {
				storeFile file(properties.getProperty("KEYSTORE_PATH"))
				storePassword properties.getProperty("KEYSTORE_PASSWORD")
				keyAlias properties.getProperty("KEY_ALIAS")
				keyPassword properties.getProperty("KEY_PASSWORD")
			}
		}
		release {
			if (properties.getProperty("KEYSTORE_PATH") != null) {
				storeFile file(properties.getProperty("KEYSTORE_PATH"))
				storePassword properties.getProperty("KEYSTORE_PASSWORD")
				keyAlias properties.getProperty("KEY_ALIAS")
				keyPassword properties.getProperty("KEY_PASSWORD")
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
}

kapt {
	arguments {
		arg("room.schemaLocation", "$projectDir/schemas")
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	def lifecycle_version = "1.1.1"
	implementation "android.arch.lifecycle:extensions:$lifecycle_version"
	kapt "android.arch.lifecycle:compiler:$lifecycle_version"

	def room_version = "1.1.1"
	implementation "android.arch.persistence.room:runtime:$room_version"
	kapt "android.arch.persistence.room:compiler:$room_version"

	def support_version = '27.1.1'
	implementation "com.android.support:appcompat-v7:$support_version"
	implementation "com.android.support:design:$support_version"
	implementation "com.android.support:cardview-v7:$support_version"
	implementation "com.android.support:recyclerview-v7:$support_version"

	def anko_version = '0.10.5'
	implementation "org.jetbrains.anko:anko-sdk21:$anko_version"
	implementation "org.jetbrains.anko:anko-sdk21-listeners:$anko_version"
	implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
	implementation "org.jetbrains.anko:anko-design:$anko_version"

	def okhttp_version = '3.10.0'
	implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
	implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"

	def glide_version = '4.7.1'
	implementation "com.github.bumptech.glide:glide:$glide_version"
	kapt "com.github.bumptech.glide:compiler:$glide_version"

	implementation 'androidx.core:core-ktx:0.3'
	implementation "android.arch.paging:runtime:1.0.1"
	implementation 'com.android.support.constraint:constraint-layout:1.1.2'
	implementation 'org.jsoup:jsoup:1.11.3'
	implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
	implementation 'com.github.Tunous:SwipeActionView:1.1.0'
	implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
	implementation 'com.github.mirrajabi:search-dialog:1.2.2'
	implementation 'com.github.jrvansuita:MaterialAbout:0.2.3'
	implementation 'q.rorbin:badgeview:1.1.3'
	implementation 'net.dankito.readability4j:readability4j:1.0'
	implementation 'pub.devrel:easypermissions:1.2.0'
	implementation 'com.rometools:rome-opml:1.10.0'
	implementation 'com.github.codekidX:storage-chooser:2.0.3'
}
